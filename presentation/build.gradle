apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    google()
}

android {
    namespace "com.alekseyld.collegetimetable"

    defaultConfig {
        applicationId "com.alekseyld.collegetimetable"

        minSdkVersion projectVersions.versions.androidMinSdk.get().toInteger()
        compileSdk projectVersions.versions.androidCompileSdk.get().toInteger()
        targetSdkVersion projectVersions.versions.androidTargetSdk.get().toInteger()

        versionCode projectVersions.versions.versionCode.get().toInteger()
        versionName projectVersions.versions.versionName.get()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        resources {
            excludes += ['LICENSE.txt', 'META-INF/DEPENDENCIES', 'META-INF/ASL2.0', 'META-INF/NOTICE', 'META-INF/LICENSE']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    lint {
        abortOnError false
        disable 'InvalidPackage', 'OldTargetApi', 'IconDensities', 'IconMissingDensityFolder'
        ignoreWarnings true
        quiet true
    }
}

dependencies {
    implementation project(':data')
    implementation project(':domain')

    implementation libs.material
    implementation libs.androidx.appcompat
    implementation libs.androidx.cardview
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.recyclerview

    annotationProcessor libs.dagger.compiler
    implementation libs.dagger
    compileOnly libs.jsr250.api

    implementation libs.rxjava
    implementation libs.rxandroid

    implementation libs.jsoup
    implementation libs.gson

    implementation libs.retrofit
    implementation libs.retrofit.gson
    implementation libs.retrofit.rxjava

    implementation libs.evernote.android.job

    implementation(platform(libs.firebase.bom))
    implementation(libs.bundles.firebase)

    testImplementation libs.junit

    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.androidx.rules
}

tasks.register('testAll') {
    dependsOn[':data:test', ':data:connectedAndroidTest', 'test', 'connectedAndroidTest']
    group = 'custom_tasks'
    description = "Run all tests"
}
